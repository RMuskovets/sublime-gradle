import groovy.json.*
import org.cyberneko.html.parsers.SAXParser

/**
 * External dependencies for the build script
 * @see http://www.gradle.org/docs/current/userguide/userguide_single.html#sec:external_dependencies
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.1.6',
                    'net.sourceforge.nekohtml:nekohtml:1.9.18'
    }
}

archivesBaseName = 'sublime-gradle'

ext {
    gradleDoc = 'http://www.gradle.org/docs/current/dsl'
    topLevelCompletion = [] as Set
}

task createCompletions << {
    cleanFiles()

    def parser = new XmlSlurper(new org.cyberneko.html.parsers.SAXParser())

    def links = findAllApiLinks(parser)

    links.each { link ->
        if (link =~ "api.Project") {
            createCompletionsForProject(parser, link)
        } else {
            createCompletionsForTask(parser, link)
        }
    }
}

def cleanFiles() {
    file('./completions').deleteDir()
    file('./completions').mkdir()
    file('./task-patterns.yml').delete()
}

def createCompletionsForProject(parser, link) {
    println "Creating top-level completions from Project"

    def json = new JsonBuilder()
    def items = getLiteralsFromLink(parser, link)

    writeCompletions(json, "source.gradle", items)
    writeHighlightingInfoForProject(items)
}

def createCompletionsForTask(parser, link) {
    println "Creating completions for ${link}"
    
    def json = new JsonBuilder()
    def taskName = link - "org.gradle.api.tasks." - ".html"
    def items = getLiteralsFromLink(parser, link)
    
    writeCompletions(json, "${taskName}.tasks.body.source.gradle", items)
    writeHighlightingInfoForTask(taskName, items)
    addToTopLevelCompletions(taskName)

    items.clear() //TODO: Probably safe to delete this now, yeah?
}

def getLiteralsFromLink(parser, link) {
    def literals = [] as Set
    parser.parse("$gradleDoc/$link").'**'.findAll {
        it.name() == 'H4' && it.@class == 'signature'
    }.each {
        it.CODE.findAll {
            it.@class == 'literal'
        }.each {
            literals << it.text()
        }
    }
    return literals
}

def writeCompletions(json, scopeName, items) {
    def completionMap = []
    items.each {
        completionMap << [trigger: it, contents:it]
    }

    json scope: scopeName, completions: completionMap
    file("./completions/${scopeName}.sublime-completions").write(json.toPrettyString())
}

def writeHighlightingInfoForTask(taskName, items) {
    file('./task-patterns.yml').withWriterAppend { out ->
        out.println "- name: ${taskName}.task.source.gradle"
        out.println "  begin: '\\s*(task)\\s+(\\w+)\\s*\\(.*type: ${taskName}.*\\)\\s*{'"
        out.println "  beginCaptures:"
        out.println "    '1': {name: keyword.task.source.gradle}"
        out.println "    '2': {name: entity.name.function}"
        out.println "  end: '}'"
        out.println "  contentName: '${taskName}.tasks.body.source.gradle'"
        out.println "  patterns:"
        out.println "    - include: '#project-patterns'"
        items.each {
            out.println "    - name: support.function.task.${taskName}.gradle"
            out.println "      match: ${it}"
        }
        out.println "    - include: source.groovy"
        out.println ""
    }
}

def writeHighlightingInfoForProject(items) {
    file('./project-patterns.yml').withWriter { out ->
        out.println "project-patterns:"
        out.println "  patterns:"
        items.each {
            out.println "    - name: support.function.project.${it}.gradle"
            out.println "      match: ${it}"
        }
    }
}

def addToTopLevelCompletions(scopeName) {

}

def findAllApiLinks(parser) {
    def links = [] as Set
    parser.parse(gradleDoc).'**'.findAll {
        it.name() == 'A' && it['@href'] =~ /^org\.gradle\.api/
    }.each {
        def link = it['@href'].toString().replaceAll(/\#.*/, '')
        links << link
    }
    return links
}

task createPackages {
    doFirst {
        assert archivesBaseName
        assert version != 'unspecified'
        ext.homepage = "https://github.com/koizuss/$archivesBaseName"
        ext.url = "${ext.homepage}/zipball/master"
    }

    doLast {
        def json = new JsonBuilder()
        json schema_version: '1.2', packages: [
            [
                "name": archivesBaseName,
                "description": "Support for use Gradle on Sublime Text 2",
                "author": "koizuss.apps@gmail.com",
                "homepage": homepage,
                "last_modified": new Date().format('yyyy-MM-dd HH:mm:ss'),
                "platforms": [
                    "*": [
                        [
                            "version": version,
                            "url": url
                        ]
                    ]
                ]
            ]
        ]

        file('./packages.json').write(json.toPrettyString())
    }
}