import groovy.json.*
import org.cyberneko.html.parsers.SAXParser

/**
 * External dependencies for the build script
 * @see http://www.gradle.org/docs/current/userguide/userguide_single.html#sec:external_dependencies
 */
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-all:2.1.6',
                    'net.sourceforge.nekohtml:nekohtml:1.9.18'
    }
}

archivesBaseName = 'sublime-gradle'

ext {
    gradleDoc = 'http://www.gradle.org/docs/current/dsl'
}

task createCompletions << {
    cleanFiles()

    def parser = new XmlSlurper(new org.cyberneko.html.parsers.SAXParser())

    def links = findAllApiLinks(parser)

    links.each { link ->
        createCompletionsForTask(parser, link)
    }
}

def cleanFiles() {
    file('./completions').deleteDir()
    file('./completions').mkdir()
    file('./task-patterns.yml').delete()
}

def createCompletionsForTask(parser, link) {
    println "Creating completions for ${link}"
    
    def json = new JsonBuilder()
    def scopeName = link - "org.gradle.api." - ".html"
    def items = getLiteralsFromLink(parser, link)
    
    writeCompletions(json, scopeName, items)
    writeHighlightingInfo(scopeName, items)

    items.clear()
}

def getLiteralsFromLink(parser, link) {
    def literals = [] as Set
    parser.parse("$gradleDoc/$link").'**'.findAll {
        it.name() == 'H4' && it.@class == 'signature'
    }.each {
        it.CODE.findAll {
            it.@class == 'literal'
        }.each {
            literals << it.text()
        }
    }
    return literals
}

def writeCompletions(json, scopeName, items) {
    def completionMap = []
    items.each {
        completionMap << [trigger: it, contents:it]
    }

    json scope: "${scopeName}.body.source.gradle", completions: completionMap
    file("./completions/${scopeName}.sublime-completions").write(json.toPrettyString())
}

def writeHighlightingInfo(scopeName, items) {
    file('./task-patterns.yml').withWriterAppend { out ->
        out.println "- name: ${scopeName - 'tasks.'}.task.source.gradle"
        out.println "  begin: '\\s*(task)\\s+(\\w+)\\s*\\(.*type: ${scopeName - 'tasks.'}.*\\)\\s*{'"
        out.println "  beginCaptures:"
        out.println "    '1': {name: keyword.task.source.gradle}"
        out.println "    '2': {name: entity.name.function}"
        out.println "  end: '}'"
        out.println "  contentName: '${scopeName}.body.source.gradle'"
        out.println "  patterns:"
        items.each {
            out.println "    - name: support.function.task.${scopeName}.gradle"
            out.println "      match: ${it}"
        }
        out.println "    - include: source.groovy"
        out.println ""
    }
}

def findAllApiLinks(parser) {
    def links = [] as Set
    parser.parse(gradleDoc).'**'.findAll {
        it.name() == 'A' && it['@href'] =~ /^org\.gradle\.api/
    }.each {
        def link = it['@href'].toString().replaceAll(/\#.*/, '')
        links << link
    }
    return links
}

task createPackages {
    doFirst {
        assert archivesBaseName
        assert version != 'unspecified'
        ext.homepage = "https://github.com/koizuss/$archivesBaseName"
        ext.url = "${ext.homepage}/zipball/master"
    }

    doLast {
        def json = new JsonBuilder()
        json schema_version: '1.2', packages: [
            [
                "name": archivesBaseName,
                "description": "Support for use Gradle on Sublime Text 2",
                "author": "koizuss.apps@gmail.com",
                "homepage": homepage,
                "last_modified": new Date().format('yyyy-MM-dd HH:mm:ss'),
                "platforms": [
                    "*": [
                        [
                            "version": version,
                            "url": url
                        ]
                    ]
                ]
            ]
        ]

        file('./packages.json').write(json.toPrettyString())
    }
}